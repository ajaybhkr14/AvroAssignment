plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id "com.github.davidmc24.gradle.plugin.avro-base" version "1.0.0"
}

group 'org.fretron'
version '1.0-SNAPSHOT'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    implementation "org.apache.avro:avro:1.10.0"
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.32'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-avro:2.12.0-rc2'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.29'
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Common',
                'Implementation-Version': version,
                'Main-Class': 'integrationTests.CommonRunner'
    }
    baseName = project.name + '-all'
    from sourceSets.test.output
//collect all dependencies
    from { configurations.testRuntime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    exclude('META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA')
}


task renewAvro {
    doLast {
        delete "src/main/java/com/fretron/model"
        copy {
            from "build/generated-main-avro-java/com/fretron/model"
            into "src/main/java/com/fretron/model"
        }
    }
}


task generateAvro(type: com.github.davidmc24.gradle.plugin.avro.GenerateAvroJavaTask) {
    source("src/main/avro")
    include("**/**/*.avsc")
    outputDir = file("src/main/java")
}

renewAvro.dependsOn generateAvro
build.dependsOn renewAvro

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
avro{
    fieldVisibility = "PRIVATE"
}